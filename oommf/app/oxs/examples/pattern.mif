# MIF 2.1
# MIF Example File: pattern.mif

# Description: Example of a patterned array of two distinct species,
# both magnetically active.  Oxs_ScriptAtlas is used to provide
# pointwise variation in material parameters.
#
set pi [expr {4*atan(1.0)}]
set mu0 [expr {4*$pi*1e-7}]

RandomSeed 1

Parameter xrange 600e-9
Parameter yrange 300e-9
Parameter zrange  20e-9

Parameter xperiod 60e-9
Parameter yperiod 60e-9

Parameter x1 50e-9
Parameter y1 25e-9
Parameter x2 25e-9
Parameter y2 50e-9

# Material parameters
set Ms1    8e5 ;# Saturation magnetization for species 1
set A1  13e-12 ;# Exchange coupling for species 1
set K1     0.0 ;# Uniaxial anisotropy constant for species 1

set Ms2   14e5 ;# Saturation magnetization for species 2
set A2  30e-12 ;# Exchange coupling for species 2
set K2   520e3 ;# Uniaxial anisotropy constant for species 2

proc Pattern { x y z } {
   global xperiod yperiod x1 y1 x2 y2

   # Determine which pattern cell (x,y) is in
   set xcell [expr {int(floor($x/$xperiod))}]
   set ycell [expr {int(floor($y/$yperiod))}]
   if {($xcell + $ycell) % 2 == 1} {
      # Empty cell
      return 0
   }

   # Compute offsets from center of cell
   set xoff [expr {abs($x - ($xcell+0.5)*$xperiod)}]
   set yoff [expr {abs($y - ($ycell+0.5)*$yperiod)}]

   if {$xcell %2 == 0 } {
      # Pattern 1 cell
      if {2*$xoff < $x1 && 2*$yoff < $y1} {
         return 1  ;# Point is inside pattern 1 region
      }
      return 0 ;# Point is outside pattern 1 region
   }

   # Otherwise, pattern 2 cell
   if {2*$xoff < $x2 && 2*$yoff < $y2} {
      return 2  ;# Point is inside pattern 2 region
   }
   return 0 ;# Point is outside pattern 2 region
}

Parameter xycellsize  5e-9
Parameter  zcellsize  5e-9

Specify Oxs_ScriptAtlas:atlas [subst {
   xrange {0 $xrange}
   yrange {0 $yrange}
   zrange {0 $zrange}
   regions { P1 P2 }
   script Pattern
   script_args rawpt
}]

Specify Oxs_RectangularMesh:mesh [subst {
  cellsize {$xycellsize $xycellsize $zcellsize}
  atlas :atlas
}]

Specify Oxs_Exchange6Ngbr [subst {
   atlas :atlas
   default_A 0.0
   A {
      P1 P1 $A1
      P2 P2 $A2
   }
}]

Specify Oxs_UniaxialAnisotropy [subst {
 K1 { Oxs_AtlasScalarField {
    atlas :atlas
    default_value 0.0
    values {
       P1 $K1
       P2 $K2
    }
 }}
 axis {0 0 1}
}]

Specify Oxs_UZeeman [subst {
  multiplier [expr {0.001/$mu0}]
  Hrange {
     {   0   0   0   100   0   0   2 }
     { 100   0   0  -100   0   0   2 }
     {   0   0   0     0 100   0   4 }
     {  10  10  10    50  50  50   0 }
  }
}]

Specify Oxs_Demag {}

Specify Oxs_CGEvolve:evolve {}

Specify Oxs_MinDriver [subst {
 evolver :evolve
 stopping_mxHxm 1e-3
 mesh :mesh
 Ms { Oxs_AtlasScalarField {
    atlas :atlas
    default_value 0.0
    values {
       P1 $Ms1
       P2 $Ms2
    }
 }}
 m0 {0.99498744 0.1 0}
}]
