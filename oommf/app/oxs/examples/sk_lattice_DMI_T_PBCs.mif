# MIF 2.1 ---------------------------------------------------------------------

# Generate a skyrmion lattice in a "unit cell" with Periodic Boundaries
#
# Authors: D. Cortes, M. Beg, H. Fangohr (2018)
#
# We set 128x64x20 mesh cells in a 150 x 150/sqrt(3) x 20 nm^3 sample
# and initialise the system with hexagonally arranged regions where skyrmions
# are going to be stabilised. We use periodicity only in the XY direction 

set PI [expr {4 * atan(1.)}]
set MU0 [expr {4 * $PI * 1e-7}]

# -----------------------------------------------------------------------------
# FeGe
set Ms [expr {3.84e5}]
set A  [expr {8.78e-12}]
set D  [expr {1.58e-3}]
set BZ [expr {0.35}]

set XL [expr {150e-9}]
set YL [expr {$XL / sqrt(3)}]
set ZL [expr {20e-9}]

set xcell [expr {$XL / 128}]
set ycell [expr {$YL / 64}]
set zcell [expr {2e-9}]

# -----------------------------------------------------------------------------

# BoxAtlas
Specify Oxs_BoxAtlas:atlas [subst {
  xrange {0 $XL}
  yrange {0 $YL}
  zrange {0 $ZL}
  name atlas
}]

# RectangularMesh
Specify Oxs_PeriodicRectangularMesh:mesh [subst {
  cellsize {$xcell $ycell $zcell}
  atlas Oxs_BoxAtlas:atlas
  periodic "xy"
}]

# UniformExchange
Specify Oxs_UniformExchange [subst {
  A $A
}]

# BulkDMI
Specify Oxs_DMI_T [subst {
  default_D $D
  atlas :atlas
  D {
    atlas atlas $D
  }
}]

Specify Oxs_FixedZeeman:Bfield [subst {
  comment {Field values in Tesla; scale to A/m}
  multiplier [expr {1 / $MU0}]
  field {0.0 0.0 $BZ}
}]

# CGEvolver
Specify Oxs_CGEvolve {}

# MinDriver
Specify Oxs_MinDriver [subst {
  evolver Oxs_CGEvolve
  stopping_mxHxm 0.01
  mesh :mesh
  Ms $Ms
  m0 { Oxs_ScriptVectorField {
     atlas :atlas
     script { skX }
     norm 1.0
     script_args { relpt }
  }}
 
  basename skL_T_PBCs
  scalar_field_output_format {text %\#.15g}
  vector_field_output_format {text %\#.15g}
}]

Destination table mmArchive
Destination mags mmArchive

Schedule DataTable table Stage 1
Schedule Oxs_MinDriver::Magnetization mags Stage 1

# =============================================================================

proc skX { x y z } {
	set xnorm  [expr {(2 * $x - 1)}]
	set ynorm  [expr {(2 * $y - 1)}]

    foreach xsk [list -1.0 1.0 -1.0 1.0 0.0] ysk [list -1.0 -1.0 1.0 1.0 0.0] {

        set xrel [expr {$xnorm - $xsk}]
        set yrel [expr {$ynorm - $ysk}]
        set radius [expr {sqrt($xrel * $xrel + $yrel * $yrel)}]

        if {$radius < 0.3} { return [list 0 0 -1] }

    }
    
    return [list 0 0 1]
}
