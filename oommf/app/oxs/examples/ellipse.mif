# MIF 2.1
# MIF Example File: ellipse.mif
# Description: Hysteresis loop of an elliptical cylinder.

set pi [expr {4*atan(1.0)}]
set mu0 [expr {4*$pi*1e-7}]

Parameter invert 0
Parameter axis z
Parameter margin 0

if {!$invert} {
   # Active region is the interior of the ellipsoid
   Specify Oxs_EllipseAtlas:atlas [subst {
      xrange {0 320e-9}
      yrange {0 160e-9}
      zrange {0  80e-9}
      axis $axis
      margin [list $margin]
      name ellipse
   }]
} else {
   # Active region is the interior of the ellipsoid
   Specify Oxs_EllipseAtlas:atlas [subst {
      xrange {0 320e-9}
      yrange {0 160e-9}
      zrange {0  80e-9}
      axis $axis
      margin [list $margin]
      name {universe ellipse}
   }]
}

Specify Oxs_RectangularMesh:mesh {
  cellsize {5e-9 5e-9 5e-9}
  atlas :atlas
}

# Exchange
Specify Oxs_UniformExchange {
  A   1.3e-11
}

# Demag
Specify Oxs_Demag {}

# Applied field
Specify Oxs_UZeeman [subst {
  multiplier [expr {0.001/$mu0}]
  Hrange {
     {     0    0   0   200   20   20   10 }
     {  200    20  20  -200  -20  -20   20 }
     { -200   -20 -20   200   20   20   20 }
  }
}]

Specify Oxs_CGEvolve:evolve {}

proc Vortex { x y z } {
    set xrad [expr {$x-0.5}]
    set yrad [expr {$y-0.5}]
    set normsq [expr {$xrad*$xrad+$yrad*$yrad}]
    if {$normsq <= 0.0125} {return "0 0 1"}
    return [list $yrad [expr {-1*$xrad}] 0]
}

Specify Oxs_MinDriver {
 basename ellipse
 evolver :evolve
 stopping_mxHxm 0.1
 mesh :mesh
 Ms { Oxs_AtlasScalarField {
    atlas :atlas
    default_value 0.0
    values {
       ellipse 8e5
    }
 }}
 m0 { Oxs_ScriptVectorField {
    atlas :atlas
    script Vortex
    norm  1
 }}
}
