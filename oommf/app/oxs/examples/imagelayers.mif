# MIF 2.1
# MIF Example File: imagelayers.mif
# Description: Example use of multiple Oxs_ImageAtlas objects, one per
#              layer, embedded into an Oxs_MultiAtlas.
set pi [expr {4*atan(1.0)}]
set mu0 [expr {4*$pi*1e-7}]

RandomSeed 1

# Create initialization string for Oxs_MultiAtlas containing three
# Oxs_ImageAtlas objects at different layer heights.
# NB: Tk is required to read GIF files.
set atlaslayers {}
for {set i 0} {$i<3} {incr i} {
   set za [expr {$i*6e-9}]
   set zb [expr {($i+1)*6e-9}]
   lappend atlaslayers atlas [subst {
      Oxs_ImageAtlas {
         xrange {0 240e-9}
         yrange {0 150e-9}
         zrange {$za $zb}
         viewplane "xy"
         image layer$i.gif
         colormap {
            black nonmagnetic
            green cobalt
            red   iron
         }
         matcherror .1
      }
   }]
}
# For development purposes, print the contents of atlaslayers to the
# Oxsii "console" window.  (The console window can be opened via
# Oxsii | File | Show Console.) The Report command can be commented out
# or deleted once the MIF file works.
Report $atlaslayers

# Create Oxs_MultiAtlas. Note atlas name "atlas".
Specify Oxs_MultiAtlas:atlas $atlaslayers

Specify Oxs_RectangularMesh:mesh {
  cellsize {3e-9 3e-9 3e-9}
  atlas :atlas
}

Specify Oxs_ExchangePtwise {
  A  { Oxs_AtlasScalarField {
         atlas :atlas
         default_value 0
         values {
            cobalt 30e-12
            iron   21e-12
         }
  }}
}

# Anisotropy for cobalt region
Specify Oxs_UniaxialAnisotropy {
   K1 { Oxs_AtlasScalarField {
      atlas :atlas
      default_value 0.0
      values {
         cobalt 520e3
      }
   }}
   axis { 0 0 1 }
}

# Anisotropy for iron region
Specify Oxs_CubicAnisotropy {
   K1 { Oxs_AtlasScalarField {
      atlas :atlas
      default_value 0.0
      values {
         iron 48e3
      }
   }}
   axis1 { 1 0 0 }
   axis2 { 0 1 0 }
}

Specify Oxs_UZeeman [subst {
  multiplier [expr {0.001/$mu0}]
  Hrange {
      { 0  0  0   100  0  0   5}
  }
}]

Specify Oxs_Demag {}

Specify Oxs_CGEvolve {}

Specify Oxs_MinDriver {
 basename imagelayers
 evolver Oxs_CGEvolve
 stopping_mxHxm 0.1
 mesh :mesh
 Ms { Oxs_AtlasScalarField {
   atlas :atlas
   default_value 0
   values {
      cobalt 1400e3
      iron   1700e3
   }
 }}
 m0 { Oxs_RandomVectorField {
    min_norm 1.0
    max_norm 1.0
 }}
}
